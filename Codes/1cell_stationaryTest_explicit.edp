// Look for stationary solution of 1cell eq (1.1)- explicit
// Newton's method

/// Parameters:
string prm = "V2 matlab"; // "Set1", "V1 matlab", "V2 matlab"
include "PRM.edp";

load "ffrandom";
load "iovtk";
int[int] ffordervel = [1]; // to save images in .vtk format
int seed = 10;
int ld = 0;
string path = "results/2021-10-20_10-56-37/";  // path of the previously computed U, V

Xh U, V, // U_k+1, V_k+1
   U0, V0, // U_k, V_k
   incrU, incrV;

// Linearized problem: matrices and vectors
varf au(u,phi) = int2d(Th) (-tildeD1 * gradS(u)'*gradS(phi) + tildea1 * u*phi);
varf bu(v,phi) = int2d(Th) (tildeb1 * v*phi);
varf cuu(u,phi) = int2d(Th) (2 * tildec1 * U0 * V0 * u * phi) +
                  int2d(Th) (2 * tildec1 * U0 * V0 * U0 * phi); //rhs1
varf cuv(v,phi) = int2d(Th) (tildec1 * U0 * U0 * v * phi);

varf av(v,phi) = int2d(Th) (-tildeD2 * gradS(v)'*gradS(phi) + tildea2 * v*phi);
varf bv(u,phi) = int2d(Th) (tildeb2 * u*phi);
varf cvu(u,phi) = int2d(Th) (2 * tildec2 * U0 * V0 * u * phi) +
                  int2d(Th) (2 * tildec2 * U0 * V0 * U0 * phi) - int2d(Th) (1/tau * phi); //rhs2
varf cvv(v,phi) = int2d(Th) (tildec2 * U0 * U0 * v * phi);

matrix Au = au(Xh,Xh);
matrix Av = av(Xh,Xh);
matrix Bu = bu(Xh,Xh);
matrix Bv = bv(Xh,Xh);
matrix tmpu1, tmpu2, tmpv1, tmpv2;
matrix Cuu, Cuv, Cvu, Cvv; // nedd to be updated at every Newton it
real[int] rhs1(dimXh),
          rhs2(dimXh),
          tmp1(2*dimXh);

matrix A;
real[int] F(2*dimXh);

//step of the Newton method: from Uold,Vold (u_k,V_k) -> find U,V (U_k+1, V_k+1))
func int step()
{
  // update time-dep matrices and vectors
  Cuu = cuu(Xh,Xh);
  Cuv = cuv(Xh,Xh);
  Cvu = cvu(Xh,Xh);
  Cvv = cvv(Xh,Xh);
  rhs1 = cuu(0,Xh);
  rhs2 = cvu(0,Xh);

  tmpu1 = Au + Cuu;
  tmpu2 = Bu + Cuv;
  tmpv1 = Bv + Cvu;
  tmpv2 = Av + Cvv;
  A = [[tmpu1, tmpu2],
       [tmpv1, tmpv2]];
  //set solver for matrices
  set(A, solver = UMFPACK);
  F = [rhs1,rhs2];

  //try2
  tmp1 = A^-1 * F;
  for(int ii = 0; ii < Xh.ndof; ii++)
  {
    U[][ii] = tmp1[ii];
    V[][ii] = tmp1[ii + Xh.ndof];
  }

  return 0;
}

//return error estimate: hyp. residual(normalized) with L2 norm
func real residual()
{
  // compute the increment between (U,V) and (U0,V0)
  incrU[] = U[];
  incrU[] -= U0[];

  incrV[] = V[];
  incrV[] -= V0[];

  //calcolo residuo normalizzato con norma L2
  real errU = int2d(Th) ((incrU)^2);
  real errV = int2d(Th) ((incrV)^2);
  real normU = int2d(Th) ((U0)^2);
  real normV = int2d(Th) ((V0)^2);
  // potrebbero essere nulle
  cout << "#check norma U" << normU << endl;
  cout << "#check norma V" << normV << endl;
  cout << "#check norma U INF" << U0[].linfty << endl;
  cout << "#check norma V INF" << V0[].linfty << endl;

  cout << "#check norma incrU INF " << incrU[].linfty << endl;
  cout << "#check norma incrV INF " << incrV[].linfty << endl;

  return sqrt(errU/(normU+1e-12)) + sqrt(errV/(normV+1e-12));
}

//Initialization: eh qua parte critica
if (ld) {
  ifstream fU(path + "lastU.txt");
  ifstream fV(path + "lastV.txt");

  fU >> U[];
  fV >> V[];

  plot(U, fill = 1, value = 1, cmm = "Loaded Active ROPS", wait = 1);
  plot(V, fill = 1, value = 1, cmm = "Loaded Inactive ROPS", wait = 1);
}
else
{
  U = u0;
  V = v0;
  // srandom(seed);
  // // ranreal 1 generates a random number in [0,1]
  //   for(int ii = 0; ii < Xh.ndof; ii++)
  //   {
  //     U[][ii] += randreal1();
  //     V[][ii] += randreal1();
  //   }

  U[] += pert[];
  V[] += pert[];
}

plot(U, value =1, fill =1/*, wait=1*/, cmm="Concentration of ACTIVE ROPs perturbed", ps = "results/U" + iter + ".ps");
plot(V, value =1, fill =1/*, wait=1*/, cmm="Concentration of INACTIVE ROPs perturbed", ps = "results/V" + iter + ".ps");
savevtk("results/U" + iter + ".vtk", Th, U, dataname = "Active_ROPs", order = ffordervel);
savevtk("results/V" + iter + ".vtk", Th, V, dataname = "Inactive_ROPs", order = ffordervel);
ofstream foutU("results/U" + iter + ".txt");
ofstream foutV("results/V" + iter + ".txt");
foutU << U[];
foutV << V[];
U0 = U;
V0 = V;

/// Main: Newton loop
err = tol + 1; // at least one iteration has to be performed
for(iter = 1; iter < MAXiter /*&& err > tol*/; ++iter)
{
  //perform an iteration of the linerized solver
  int errc = step(); // -> find U,V
  if (errc != 0)
  {
    cout << "ERROR" << endl;
    break;
  }

  //update err for the stp criterion
  err = residual(); //-> compute the residual between U,V and U0, V0
  cout << "#err: " << err << endl;

  /// update old solution
  U0 = U;
  V0 = V;

  plot(U, value =1, fill =1, cmm="Concentration of ACTIVE ROPs at ITERATION "+ iter, ps = "results/U" + iter + ".ps");
  plot(V, value =1, fill =1, cmm="Concentration of INACTIVE ROPs at ITERATION"+ iter, ps = "results/V" + iter + ".ps");
  savevtk("results/U" + iter + ".vtk", Th, U, dataname = "Active_ROPs", order = ffordervel);
  savevtk("results/V" + iter + ".vtk", Th, V, dataname = "Inactive_ROPs", order = ffordervel);

  // Output file for last results -> to be loaded in case of interuption
  ofstream foutU("results/U" + iter + ".txt");
  ofstream foutV("results/V" + iter + ".txt");
  foutU << U[];
  foutV << V[];

}


/// Postprocessing
cout << "# nonlinear iter: " << iter << endl;
cout << "# residual: " << err << endl;

plot(U, value =1, fill =1, wait=1, cmm="Concentration of ACTIVE ROPs");
plot(V, value =1, fill =1, wait=1, cmm="Concentration of INACTIVE ROPs");
